# Maintainer: Will Reed <wreed@disroot.org>

_pkgname=vendr
pkgname=${_pkgname}-git
pkgver=0.2.1.r17.g1ce0a6d
pkgrel=1
pkgdesc="Easy way to vendor your projects' dependencies"
arch=('x86_64')
url=https://github.com/wreedb/vendr
license=('GPL-3.0-or-later')
source=("git+${url}.git")
b2sums=('SKIP')
provides=('vendr')
depends=(libgit2 curl tomlplusplus)
makedepends=(meson gcc scdoc)
conflicts=("${_pkgname}")

_srcdir="${_pkgname}"

pkgver() {
  cd ${_srcdir}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd ${_srcdir}
    meson setup .build \
        --prefix=/usr \
        --buildtype=release \
        -D tomlpp-header-only=false \
        -D cpp_args="${CXXFLAGS}" \
        -D cpp_link_args="${LDFLAGS}" \
        -D install-docs=false
}

build() {
    cd ${_srcdir}
    meson compile -C .build
}

package() {
    cd ${_srcdir}
    meson install -C .build --destdir ${pkgdir}
    install -m 644 examples/*.toml -Dt ${pkgdir}/usr/share/doc/${pkgname}
    install -dm 0755 ${pkgdir}/usr/share/licenses/${pkgname}
    cp -a LICENSE.md licenses/* -t ${pkgdir}/usr/share/licenses/${pkgname}
}